<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" 
	layout="absolute" creationComplete="init()"
	backgroundColor="0xffffff" paddingBottom="0" paddingLeft="0" 
	paddingRight="0" paddingTop="0">
	
	<mx:Script>
		<![CDATA[
		
		import mx.events.FlexEvent;

		public function displayText(text:String):void {
			textWidget.text = text;
		}		
			
		private function init():void {
			addEventListener(FlexEvent.APPLICATION_COMPLETE, onApplicationComplete);
			addJSCallback("displayText", displayText);
			addJSCallback("addSendListener", addSendListener);
		}
		
		private function onApplicationComplete(event:FlexEvent):void {
				callLater(function():void {
					ExternalInterface.call("_swf_application_complete", swfID);
				});
			}
		
		public static function addJSCallback(jsFunctionName:String, flexFunction:Function):void {
	    	try {
	    		if (ExternalInterface.available) {
					ExternalInterface.addCallback(jsFunctionName, flexFunction);
	    		}
			} catch (error:SecurityError) {
				trace("Couldn't add javascript callback: " + error);
			}
	    } 
	    
		public function addSendListener(jsFunctionName:String):void {
			sendButton.addEventListener(MouseEvent.CLICK, function(event:MouseEvent):void {
				ExternalInterface.call(jsFunctionName, swfID, textWidget.text);				
			});
		}
	    
		public function get swfID():String {
			return Application.application.parameters.swfid;
		}

		]]>
	</mx:Script>
	
	<mx:Button id="sendButton" x="200" width="80" label="Send"/>
	<mx:TextInput id="textWidget" x="0" width="200"/>
	
</mx:Application>
